#import "../MenuController.h"
#import "../PortMenu.hpp"
#include "../AppIdentity.hpp"
#import <Cocoa/Cocoa.h>
#import <Foundation/Foundation.h>
#include <cstddef>
#include <cstdint>
#include <objc/NSObject.h>
#include <phosg/Image.hh>

NSMenu* MCCreateMenu(const MenuList& menuList);
NSMenu* MCCreateSubMenu(NSString* title, const Menu& menuRes, const std::list<std::shared_ptr<Menu>> submenus);

// We have to forward declare because of PixMap collision w/QuickDraw
phosg::ImageRGBA8888N DecodeCIconImage(int16_t iconID);

static constexpr char kDiamondMark = 19;

// Wraps a decoded cicn as an NSImage, NN scale 2x, byteswap BGRA->RGBA.
static constexpr NSInteger kMenuIconUpscale = 2;

static NSImage* MCImageForCicn(int16_t cicnID) {
  if (cicnID == 0) {
    return nil;
  }
  try {
    auto decoded = DecodeCIconImage(cicnID);
    NSInteger srcW = decoded.get_width();
    NSInteger srcH = decoded.get_height();
    if (srcW <= 0 || srcH <= 0) {
      return nil;
    }
    NSInteger dstW = srcW * kMenuIconUpscale;
    NSInteger dstH = srcH * kMenuIconUpscale;
    NSBitmapImageRep* rep = [[NSBitmapImageRep alloc]
        initWithBitmapDataPlanes:NULL
                      pixelsWide:dstW
                      pixelsHigh:dstH
                   bitsPerSample:8
                 samplesPerPixel:4
                        hasAlpha:YES
                        isPlanar:NO
                  colorSpaceName:NSDeviceRGBColorSpace
                    bitmapFormat:0
                     bytesPerRow:dstW * 4
                    bitsPerPixel:32];
    if (rep == nil) {
      return nil;
    }
    const uint32_t* src = decoded.get_data();
    uint32_t* dst = reinterpret_cast<uint32_t*>([rep bitmapData]);
    for (NSInteger y = 0; y < dstH; y++) {
      const uint32_t* srcRow = src + (y / kMenuIconUpscale) * srcW;
      uint32_t* dstRow = dst + y * dstW;
      for (NSInteger x = 0; x < dstW; x++) {
        uint32_t pixel = srcRow[x / kMenuIconUpscale];
        if constexpr (!phosg::IS_BIG_ENDIAN) {
          pixel = __builtin_bswap32(pixel);
        }
        dstRow[x] = pixel;
      }
    }
    [rep setSize:NSMakeSize(srcW, srcH)];
    NSImage* image = [[NSImage alloc] initWithSize:NSMakeSize(srcW, srcH)];
    [image addRepresentation:rep];
    return image;
  } catch (const std::exception&) {
    return nil;
  }
}

@interface MCMenuItemIdentifier : NSObject

@property(readonly) int16_t menuID;
@property(readonly) int16_t itemID;

@end

@implementation MCMenuItemIdentifier

- (id)initWithRawIds:(int16_t)menu_id itemId:(int16_t)item_id {
  if (self = [super init]) {
    _menuID = menu_id;
    _itemID = item_id;
  }
  return self;
}

@end

@interface MCMenuBar : NSObject <NSMenuDelegate>

@property(readonly) NSMenu* menuObject;
@property(nonatomic) void (*callback)(int16_t, int16_t);

@end

@implementation MCMenuBar

@synthesize callback;

- (id)initWithMenuListCallback:(const MenuList&)menuList callback:(void (*)(int16_t, int16_t))_callback {
  if (self = [super init]) {
    [self MCCreateMenu:menuList];
    callback = _callback;
  }
  return self;
}

- (IBAction)MCHandleMenuClick:(id)sender {
  id identifier = [sender representedObject];
  callback([identifier menuID], [identifier itemID]);
}

- (IBAction)MCHandlePortItem:(id)sender {
  PortMenu_Apply((int)[sender tag]);
}

- (void)MCCreateMenu:(const MenuList&)menuList {
  _menuObject = [[NSMenu alloc]
      initWithTitle:[NSString stringWithUTF8String:
                        realmz::app::kProductName.data()]];
  [_menuObject setAutoenablesItems:NO];

  for (auto menu : menuList.menus) {
    NSString* title = [NSString stringWithCString:menu->title.c_str() encoding:NSMacOSRomanStringEncoding];
    NSMenuItem* menuItem = [[NSMenuItem alloc] initWithTitle:title action:NULL keyEquivalent:@""];
    menuItem.enabled = menu->enabled;
    [_menuObject addItem:menuItem];
    if (menu->items.size()) {
      NSMenu* subMenu = [self MCCreateSubMenu:title parentMenu:*menu submenus:menuList.submenus];
      [_menuObject setSubmenu:subMenu forItem:menuItem];
    }
  }

  [self addPortMenu];
}

- (void)addPortMenu {
  NSMenuItem* portItem = [[NSMenuItem alloc] initWithTitle:@"Port" action:NULL keyEquivalent:@""];
  [_menuObject addItem:portItem];
  NSMenu* portMenu = [[NSMenu alloc] initWithTitle:@"Port"];
  [portMenu setAutoenablesItems:NO];
  portMenu.delegate = self;

  NSMenuItem* filteringItem = [[NSMenuItem alloc] initWithTitle:@"Filter" action:NULL keyEquivalent:@""];
  [portMenu addItem:filteringItem];
  NSMenu* filterMenu = [[NSMenu alloc] initWithTitle:@"Filter"];
  [filterMenu setAutoenablesItems:NO];
  filterMenu.delegate = self;
  for (int i = 0; i < kPortFilterCount; i++) {
    NSMenuItem* item = [filterMenu addItemWithTitle:[NSString stringWithUTF8String:kPortFilters[i].title]
                                             action:@selector(MCHandlePortItem:)
                                      keyEquivalent:@""];
    [item setTarget:self];
    [item setTag:(NSInteger)(kPortFilterId + i)];
  }
  [portMenu setSubmenu:filterMenu forItem:filteringItem];

  NSMenuItem* scaleItem = [[NSMenuItem alloc] initWithTitle:@"Scale" action:NULL keyEquivalent:@""];
  [portMenu addItem:scaleItem];
  NSMenu* scaleMenu = [[NSMenu alloc] initWithTitle:@"Scale"];
  [scaleMenu setAutoenablesItems:NO];
  scaleMenu.delegate = self;
  for (int i = 0; i < kPortScaleCount; i++) {
    NSMenuItem* item = [scaleMenu addItemWithTitle:[NSString stringWithUTF8String:kPortScales[i].title]
                                            action:@selector(MCHandlePortItem:)
                                     keyEquivalent:@""];
    [item setTarget:self];
    [item setTag:(NSInteger)(kPortScaleId + i)];
  }
  [portMenu setSubmenu:scaleMenu forItem:scaleItem];

  NSMenuItem* aspectItem = [portMenu addItemWithTitle:@"Lock Aspect Ratio"
                                               action:@selector(MCHandlePortItem:)
                                        keyEquivalent:@""];
  [aspectItem setTarget:self];
  [aspectItem setTag:(NSInteger)kPortAspectLockId];

  [portMenu addItem:[NSMenuItem separatorItem]];
  NSMenuItem* gammaItem = [[NSMenuItem alloc] initWithTitle:@"Color Correction" action:NULL keyEquivalent:@""];
  [portMenu addItem:gammaItem];
  NSMenu* gammaMenu = [[NSMenu alloc] initWithTitle:@"Color Correction"];
  [gammaMenu setAutoenablesItems:NO];
  gammaMenu.delegate = self;
  for (int i = 0; i < kPortGammaCount; i++) {
    NSMenuItem* item = [gammaMenu addItemWithTitle:[NSString stringWithUTF8String:kPortGammaOptions[i].title]
                                           action:@selector(MCHandlePortItem:)
                                    keyEquivalent:@""];
    [item setTarget:self];
    [item setTag:(NSInteger)(kPortGammaId + i)];
  }
  [portMenu setSubmenu:gammaMenu forItem:gammaItem];

  [portMenu addItem:[NSMenuItem separatorItem]];
  NSMenuItem* presentationItem = [[NSMenuItem alloc] initWithTitle:@"Presentation" action:NULL keyEquivalent:@""];
  [portMenu addItem:presentationItem];
  NSMenu* presentationMenu = [[NSMenu alloc] initWithTitle:@"Presentation"];
  [presentationMenu setAutoenablesItems:NO];
  presentationMenu.delegate = self;
  NSArray<NSString*>* presentationTitles = @[@"Classic", @"Remastered"];
  for (int i = 0; i < kPortPresentationCount; i++) {
    NSMenuItem* item = [presentationMenu addItemWithTitle:presentationTitles[i]
                                                   action:@selector(MCHandlePortItem:)
                                            keyEquivalent:@""];
    [item setTarget:self];
    [item setTag:(NSInteger)(kPortPresentationId + i)];
  }
  [portMenu setSubmenu:presentationMenu forItem:presentationItem];

  [_menuObject setSubmenu:portMenu forItem:portItem];
}

- (void)menuNeedsUpdate:(NSMenu*)menu {
  for (NSMenuItem* item in menu.itemArray) {
    if (item.action != @selector(MCHandlePortItem:)) {
      continue;
    }
    int checked = 0, enabled = 1;
    PortMenu_ItemState((int)item.tag, &checked, &enabled);
    item.enabled = enabled ? YES : NO;
    item.state = checked ? NSControlStateValueOn : NSControlStateValueOff;
  }
}

- (NSMenu*)MCCreateSubMenu:(NSString*)title parentMenu:(const Menu&)menu submenus:(const std::list<std::shared_ptr<Menu>>)submenus {
  NSMenu* newMenu = [[NSMenu alloc] initWithTitle:title];
  [newMenu setAutoenablesItems:NO];

  int itemId = 0;
  for (auto& subMenuItemRes : menu.items) {
    itemId++;
    if (subMenuItemRes.name == "-") {
      [newMenu addItem:[NSMenuItem separatorItem]];
    } else {
      NSString* name = [NSString stringWithCString:subMenuItemRes.name.c_str() encoding:NSMacOSRomanStringEncoding];
      if (name != nullptr) {
        char key_equiv[2] = {static_cast<char>(tolower(subMenuItemRes.key_equivalent)), '\0'};
        NSString* key = [NSString stringWithCString:key_equiv encoding:NSMacOSRomanStringEncoding];
        NSMenuItem* subMenuItem = [newMenu addItemWithTitle:name action:NULL keyEquivalent:key];
        [subMenuItem setTarget:self];
        [subMenuItem setAction:@selector(MCHandleMenuClick:)];
        id menuIdentifier = [[MCMenuItemIdentifier alloc] initWithRawIds:menu.menu_id itemId:itemId];
        [subMenuItem setRepresentedObject:menuIdentifier];
        subMenuItem.enabled = subMenuItemRes.enabled;
        bool mark_is_submenu_link = (subMenuItemRes.key_equivalent == 0x1B);
        char mark = mark_is_submenu_link ? 0 : subMenuItemRes.mark_character;
        if (subMenuItemRes.checked || mark == kDiamondMark) {
          subMenuItem.state = NSControlStateValueOn;
        } else if (mark != 0) {
          subMenuItem.state = NSControlStateValueMixed;
        }
        NSImage* icon = MCImageForCicn(subMenuItemRes.icon_id);
        if (icon != nil) {
          [subMenuItem setImage:icon];
        }

        // Submenu ids are specified by the itemMark field if the itemCmd field has
        // the value 0x1B
        // Macintosh Toolbox Essentials (3-96)
        if (subMenuItemRes.key_equivalent == 0x1B && subMenuItemRes.mark_character) {
          for (auto subMenuRes : submenus) {
            if (subMenuRes->menu_id == static_cast<uint8_t>(subMenuItemRes.mark_character)) {
              NSString* subMenuTitle = [NSString stringWithCString:subMenuRes->title.c_str() encoding:NSMacOSRomanStringEncoding];
              NSMenu* subMenu = [self MCCreateSubMenu:subMenuTitle parentMenu:*subMenuRes submenus:submenus];
              [newMenu setSubmenu:subMenu forItem:subMenuItem];

              break;
            }
          }
        }
      }
    }
  }

  return newMenu;
}

@end

@interface MCPopupMenu : NSObject

@property(readonly) NSMenu* contextualMenu;
@property(nonatomic) void (*callback)(int16_t, int16_t);

@end

@implementation MCPopupMenu

@synthesize callback;

- (id)initWithWindow:(void *)nsWindow
    menu:(std::shared_ptr<Menu>)menu
    loc:(std::pair<int16_t, int16_t>)loc
    callback:(void (*)(int16_t, int16_t))_callback {
  if (self = [super init]) {
    callback = _callback;
    [self CreateMCPopupMenu:nsWindow menu:menu loc:loc];
  }
  return self;
}

- (IBAction)MCHandlePopupMenuClick:(id)sender {
  id identifier = [sender representedObject];
  callback([identifier menuID], [identifier itemID]);
}

- (void)CreateMCPopupMenu:(void *)nsWindow
    menu:(std::shared_ptr<Menu>)menu
    loc:(std::pair<int16_t, int16_t>)p {
  _contextualMenu = [[NSMenu alloc] initWithTitle:@"Contextual Menu"];
  [_contextualMenu setAutoenablesItems:NO];

  int itemId = 0;
  for(const auto& item: menu->items) {
    itemId++;
    NSString* name = [NSString stringWithCString:item.name.c_str() encoding:NSMacOSRomanStringEncoding];
    NSMenuItem *menuItem = [[NSMenuItem alloc] initWithTitle:name action:NULL keyEquivalent:@""];
    [menuItem setTarget:self];
    [menuItem setAction:@selector(MCHandlePopupMenuClick:)];
    id menuIdentifier = [[MCMenuItemIdentifier alloc] initWithRawIds:menu->menu_id itemId:itemId];
    [menuItem setRepresentedObject:menuIdentifier];
    menuItem.enabled = item.enabled;
    if (item.checked || item.mark_character == kDiamondMark) {
      menuItem.state = NSControlStateValueOn;
    } else if (item.mark_character != 0) {
      menuItem.state = NSControlStateValueMixed;
    }
    NSImage* icon = MCImageForCicn(item.icon_id);
    if (icon != nil) {
      [menuItem setImage:icon];
    }
    [_contextualMenu addItem:menuItem];
  }

  // In the Cocoa framework, the origin is the bottom left. The point p is passed to us as (top, left).
  NSWindow* window = (__bridge NSWindow*)nsWindow;
  NSView* view = window.contentView;
  NSSize size = view.frame.size;
  NSPoint loc = NSMakePoint(p.second, size.height - p.first);

  [[NSNotificationCenter defaultCenter] addObserver:self
    selector:@selector(HandlePopupMenuClosed:)
    name:NSMenuDidEndTrackingNotification
    object:_contextualMenu];

  BOOL result = [_contextualMenu popUpMenuPositioningItem:nil atLocation:loc inView:view];
}

- (void)HandlePopupMenuClosed:(NSNotification*)notification {
  [[NSNotificationCenter defaultCenter] removeObserver:self
    name:NSMenuDidEndTrackingNotification
    object:_contextualMenu];
  callback(0, 0);
}

@end

void MCSync(std::shared_ptr<MenuList> menuList, void (*callback)(int16_t, int16_t)) {
  NSApplication* application = [NSApplication sharedApplication];

  static MCMenuBar* currentMenuBar = nil;
  currentMenuBar = [[MCMenuBar alloc] initWithMenuListCallback:*menuList callback:callback];

  application.mainMenu = [currentMenuBar menuObject];
}

void MCCreatePopupMenu(void *nsWindow, std::shared_ptr<Menu> menu, std::pair<int16_t, int16_t> loc, void (*callback)(int16_t, int16_t)) {
  [[MCPopupMenu alloc] initWithWindow:nsWindow menu:menu loc:loc callback:callback];
}
